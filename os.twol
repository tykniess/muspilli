Alphabet
	a b c d e f g h i j k l m n o p q r s t u v w x y z î ê ô â ī ē ō ā ë 
	đ ƀ th:th th:d 
	¹:0 ²:0 ³:0 ⁴:0		!four contexts for Ablaut
	₄:0 ₅:0 ₇:0 		!three exceptional strong verb classes
	%{I%}:i %{I%}:0		!j-infix
	%{E%}:e %{E%}:a		!e/a alternations in dative and genitive
	%{O%}:o %{O%}:a		!o/a alternations in nominative/accusative plural
	%{M%}:m %{M%}:n		!m/n alternation in dative plural
	;

Sets
V = a e i o u %{I%}			;
FrontV = a e i %{I%}			;
BackV = o u			;
Cns = b d f g h j k l m n p w r s t v w x y z đ ƀ th ; 
HighV = i u ;

Rules

! a:b   CONSTRAINT_OPERATOR   LEFT_CONTEXT   _   RIGHT_CONTEXT ;

"Remove morpheme boundary"
%>:0 <=> _ 			;

"Umlaut in second and third class strong verbs"
!biod>u becomes biudu
!werth>u becomes wirthu
Vx:Vy <=> _  Cns+ %>:  HighV ;
     where Vx in ( o e )
           Vy in ( u i )
     matched ;

"Umlaut in sixth class strong verbs"
a:e <=> _ Cns: %>: i ; !note that this umlaut doesn't apply before [u]

"STR2 Deletion of the i in the diphtong io in ablaut grades 1 2 3 "
Vx:Vy <=> _ o: Cns+ Vz: ;
     where Vx in ( i i i )
           Vy in ( 0 0 0 )
           Vz in ( ¹ ² ³ )
     matched ;

"Grammatischer Wechsel"
Vx:Vy <=> _ Vz: ;
	where Vx in ( h th h ) !add s~r alternations
	      Vy in ( g d g )
	      Vz in ( ¹ ² ² ) 
	matched ; 

"Ablaut in preterite 1sg 3sg of strong verbs (context 1)"
Vx:Vy <=> _ Cns+ Vz: ;
     except
            ₄: _ ;
            ₅: _ ;
            ₇: _ ;
     where Vx in ( î ī o i e ê ē a )
           Vy in ( ê ê ô a a a a ô ) 
           Vz in ( ¹ ¹ ¹ ¹ ¹ ¹ ¹ ¹ ) 
     matched ;

"Ablaut in preterite pl of strong verbs (context 2)"
Vx:Vy <=> _ Cns+ Vz: ; 
     except
            ₄: _ ;
            ₅: _ ;
	    ₇: _ ;
     where Vx in ( î ī o i e ê ē a )
           Vy in ( i i u u u â ā ô )
           Vz in ( ² ² ² ² ² ² ² ² )
     matched ;

"Ablaut in participle of strong verbs (context 3)"
Vx:Vy <=> _ Cns+ Vz: ;
     except
            ₄: _ ;
            ₅: _ ;
            ₇: _ ;
     where Vx in ( î ī o i e ê ē )
           Vy in ( i i o u o o o )
           Vz in ( ³ ³ ³ ³ ³ ³ ³ )
     matched ;

"Ablaut in Strong Verbs Classes 4 and 5"
!some sort of doubling error in āles₄³>an
Vx:Vy <=> _ Cns+ (i)  Vz: Vw: %>: ;
     where Vx in ( i i i e e e u u u e i )
           Vy in ( a â o a â o a â o e e )
           Vz in ( ₄ ₄ ₄ ₄ ₄ ₄ ₄ ₄ ₄ ₅ ₅ )
           Vw in ( ¹ ² ³ ¹ ² ³ ¹ ² ³ ³ ³ )
     matched ;

"Remove j-infix before a vocalic verbal suffix"
%{I%}:0 <=> _ (¹:|²:) (>:) (¹:|²:) :i ; 

"Ablaut in Strong Verbs Class 7"
!haldan-haldu-held-heldun-gihaldan
!âhlôp₇¹
Vx:Vy <=> _ Cns+ ₇: Vw: (%>:) ;
     where Vx in ( a a â â ê  ê   ô  ô  )
           Vy in ( e e ê ê ie ie  io io )
           Vw in ( ¹ ² ¹ ² ¹  ²   ¹  ²  ) !Don't need #3 because pastpart is same as inf stem vowel
     matched ;
